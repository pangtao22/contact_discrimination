# -*- mode: cmake -*-
# vi: set ft=cmake :

find_package(PkgConfig QUIET)

pkg_check_modules(_PC_FLANN QUIET flann)

find_path(FLANN_INCLUDE_DIR
  NAMES flann/flann.hpp
  PATHS ${_PC_FLANN_INCLUDE_DIRS}
  PATH_SUFFIXES FLANN
)

find_library(FLANN_LIBRARY
  NAMES flann
  PATHS ${_PC_FLANN_LIBRARY_DIRS}
)

find_library(FLANN_CPP_LIBRARY
  NAMES flann_cpp
  PATHS ${_PC_FLANN_LIBRARY_DIRS}
)

find_library(FLANN_S_LIBRARY
  NAMES flann_s
  PATHS ${_PC_FLANN_LIBRARY_DIRS}
)

find_library(FLANN_CPP_S_LIBRARY
  NAMES flann_cpp_s
  PATHS ${_PC_FLANN_LIBRARY_DIRS}
)

set(FLANN_DEFINITIONS "${_PC_FLANN_CFLAGS_OTHER}")
set(FLANN_VERSION "${_PC_FLANN_VERSION}")

include(FindPackageHandleStandardArgs)

find_package_handle_standard_args(FLANN
  REQUIRED_VARS FLANN_INCLUDE_DIR FLANN_LIBRARY
  VERSION_VAR FLANN_VERSION
)

if(FLANN_FOUND)
  set(FLANN_INCLUDE_DIRS "${FLANN_INCLUDE_DIR}")
  set(FLANN_LIBRARIES "${FLANN_CPP_LIBRARY}" "${FLANN_LIBRARY}")

  mark_as_advanced(
    FLANN_INCLUDE_DIR
    FLANN_CPP_LIBRARY
    FLANN_CPP_S_LIBRARY
    FLANN_LIBRARY
    FLANN_S_LIBRARY
  )

  if(NOT TARGET flann)
    add_library(flann SHARED IMPORTED)
    set_target_properties(flann PROPERTIES
      IMPORTED_LOCATION "${FLANN_LIBRARY}"
      INTERFACE_COMPILE_OPTIONS "${FLANN_DEFINITIONS}"
      INTERFACE_INCLUDE_DIRECTORIES "${FLANN_INCLUDE_DIR}"
    )
  endif()

  if(NOT TARGET flann_cpp)
    add_library(flann_cpp SHARED IMPORTED)
    set_target_properties(flann_cpp PROPERTIES
      IMPORTED_LOCATION "${FLANN_CPP_LIBRARY}"
      INTERFACE_COMPILE_OPTIONS "${FLANN_DEFINITIONS}"
      INTERFACE_INCLUDE_DIRECTORIES "${FLANN_INCLUDE_DIR}"
    )
  endif()

  if(FLANN_S_LIBRARY AND NOT TARGET flann_s)
    add_library(flann_s STATIC IMPORTED)
    set_target_properties(flann_s PROPERTIES
      IMPORTED_LINK_INTERFACE_LANGUAGES C
      IMPORTED_LOCATION "${FLANN_S_LIBRARY}"
      INTERFACE_COMPILE_OPTIONS "${FLANN_DEFINITIONS}"
      INTERFACE_INCLUDE_DIRECTORIES "${FLANN_INCLUDE_DIR}"
    )
  endif()

  if(FLANN_CPP_S_LIBRARY AND NOT TARGET flann_cpp_s)
    add_library(flann_cpp_s STATIC IMPORTED)
    set_target_properties(flann_cpp_s PROPERTIES
      IMPORTED_LINK_INTERFACE_LANGUAGES CXX
      IMPORTED_LOCATION "${FLANN_CPP_S_LIBRARY}"
      INTERFACE_COMPILE_OPTIONS "${FLANN_DEFINITIONS}"
      INTERFACE_INCLUDE_DIRECTORIES "${FLANN_INCLUDE_DIR}"
    )
  endif()
endif()
